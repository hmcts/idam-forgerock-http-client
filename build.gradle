import io.swagger.codegen.DefaultGenerator
import io.swagger.codegen.config.CodegenConfigurator

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'io.swagger:swagger-codegen:2.4.14'
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.9.RELEASE'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
}


project.ext {
    swaggerTargetFolder = 'src/generated/java'
    swagger_annotations_version = "1.5.22"
    feign_version = "10.7.0"
    feign_form_version = "3.8.0"
    oltu_version = "1.0.1"
}

version = '2.4.3'
group = 'uk.gov.hmcts.reform.idam'

description = """idam-forgerock-http-client"""


sourceCompatibility = 11
targetCompatibility = 11


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url 'https://dl.bintray.com/hmcts/hmcts-maven'
    }
    jcenter()
}

configurations {
    // generatedCompile
    // compile.extendsFrom generatedCompile
    compile
}

dependencyManagement {
    imports {
        mavenBom 'uk.gov.hmcts.reform.idam:idam-bom:2.2.6'
    }
}

dependencies {
    implementation "org.springframework.data:spring-data-elasticsearch"
    implementation "org.springframework.data:spring-data-commons"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
    implementation "io.github.openfeign:feign-core:$feign_version"
    implementation "io.github.openfeign:feign-jackson:$feign_version"
    implementation "io.github.openfeign:feign-slf4j:$feign_version"
    implementation "io.github.openfeign.form:feign-form:$feign_form_version"
    implementation "org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:$oltu_version"
    implementation "com.brsanthu:migbase64:2.2"
    testImplementation "junit:junit"
}

def generateCodeFromSwagger(swaggerSourceFile, packageName, importMappings) {
    def config = new CodegenConfigurator()
    config.setInputSpec("file:///$projectDir/$swaggerSourceFile")
    config.setOutputDir("$buildDir")
    config.setLang('java')
    config.setAdditionalProperties([
            'generateApis'              : 'false',
            'apiPackage'                : 'uk.gov.hmcts.reform.idam.api.fr.' + packageName,
            'modelPackage'              : 'uk.gov.hmcts.reform.idam.api.fr.' + packageName + '.model',
            'invokerPackage'            : 'uk.gov.hmcts.reform.idam.api.fr.client.invoker',
            'sourceFolder'              : project.swaggerTargetFolder,
            'library'                   : 'feign',
            'dateLibrary'               : 'java8',
            'generateModelDocumentation': 'false'
    ])
    if (importMappings != null) {
        ((LinkedHashMap<String, String>) importMappings).each { k, v -> config.addImportMapping(k, v) }
    }
    new DefaultGenerator().opts(config.toClientOptInput()).generate()
}

task generateApi {
    outputs.dir("$buildDir")
    doLast {
        generateCodeFromSwagger('src/main/resources/forgerock-shared.yaml', 'shared', null)
        generateCodeFromSwagger('src/main/resources/forgerock-am-agents.yaml', 'am.agents', null)
        generateCodeFromSwagger('src/main/resources/forgerock-am-authentication.yaml', 'am.oidc', [
                'Response'  : 'feign.Response'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-am-oauth2-client.yaml', 'am.agents.oauth2', [
                'AmAgent'   : 'uk.gov.hmcts.reform.idam.api.fr.am.agents.model.AmAgent'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-am-policies.yaml', 'am.policies', [
                'ApiError'  : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.ApiError'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-am-authentication-chain.yaml', 'am.authenticate', [
                'ApiError'  : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.ApiError'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-idm-managed-assignment.yaml', 'idm.manage.assignment', [
                'PatchArray': 'uk.gov.hmcts.reform.idam.api.fr.shared.model.PatchArray',
                'Status'    : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.Status'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-idm-managed-user.yaml', 'idm.manage.user', [
                'PatchArray': 'uk.gov.hmcts.reform.idam.api.fr.shared.model.PatchArray',
                'Status'    : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.Status'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-idm-miscellaneous.yaml', 'idm.misc', [
                'ApiError'  : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.ApiError'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-idm-roles.yaml', 'idm.manage.roles', [
                'PatchArray': 'uk.gov.hmcts.reform.idam.api.fr.shared.model.PatchArray',
                'Status'    : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.Status'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-idm-selfservice-registration.yaml', 'idm.selfservice', [
                'ApiError'  : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.ApiError'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-idm-selfservice-reset.yaml', 'idm.selfservice', [
                'ApiError'  : 'uk.gov.hmcts.reform.idam.api.fr.shared.model.ApiError',
                'SubmitRequirementsRequest' : 'uk.gov.hmcts.reform.idam.api.fr.idm.selfservice.model.SubmitRequirementsRequest',
                'SuccessObject' : 'uk.gov.hmcts.reform.idam.api.fr.idm.selfservice.model.SuccessObject'
        ])
        generateCodeFromSwagger('src/main/resources/forgerock-idm-login.yaml', 'idm.login', null)
    }
}

sourceSets {
    generated {
        java {
            srcDirs "$buildDir/$swaggerTargetFolder"
        }
    }
}

jar {
    // from sourceSets.generated.output
}

/*
    compileGeneratedJava.dependsOn generateApi
    classes.dependsOn generatedClasses
    compileJava.dependsOn compileGeneratedJava
*/

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            groupId project.group
            artifactId rootProject.name
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = "${rootProject.name}"
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = "https://github.com/hmcts/${rootProject.name}"
        version {
            name = project.version
        }
    }
}
